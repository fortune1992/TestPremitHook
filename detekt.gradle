repositories {
    mavenCentral()
}

configurations {
    detekt
}

dependencies {
    detekt 'io.gitlab.arturbosch.detekt:detekt-cli:1.23.0-RC1'
    detekt 'io.gitlab.arturbosch.detekt:detekt-formatting:1.23.0-RC1'
}

task detekt(type: JavaExec) {
    description = "Kotlin static code analysis."
    classpath = configurations.detekt
    main = "io.gitlab.arturbosch.detekt.cli.Main"
    def input
    if (hasCheckCommitParam() || isReleaseBuildType()) {
        input = getDeteKtInput()
    } else {
        input = "$project.rootDir"
    }
    def config = "$rootProject.rootDir/config/kotlin/detekt-config.yml"
    def exclude = ".*/resources/.*,.*/build/.*,.*/test/.*"
    def reports = "xml:$project.buildDir/reports/detekt/kotlin-reports.xml"
    def params = ['-i', input, '-c', config, '-r', reports, "-ex", exclude]
    args(params)
}

// 有文件变更才会执行detekt(detekt input传空会导致task报错，需要用条件控制task是否执行)
detekt.onlyIf {
    if (hasCheckCommitParam() || isReleaseBuildType()) {
        !getDeteKtInput().isEmpty()
    } else {
        true
    }
}

def getDeteKtInput() {
    def changedFileNameList = findChangedFiles(getChangedFiles(), ".kt")
    StringBuilder sb = new StringBuilder()
    for (int i = 0; i < changedFileNameList.size(); i++) {
        if (i > 0) {
            sb.append(", ")
        }
        String changedFileName = changedFileNameList.get(i)
        sb.append("$project.rootDir/")
        sb.append(changedFileName)
    }
    return sb.toString()
}