apply plugin: 'checkstyle'
apply from: rootProject.file("commit.gradle")
apply from: rootProject.file("detekt.gradle")

configurations {
    checkstyle
}

checkstyle {
    configFile new File(rootProject.rootDir, 'config/checkstyle/checkstyle.xml')
    configProperties.checkstyleSuppressionFilterPath = new File(
            "${project.rootDir}/config/checkstyle/checkstyle-suppression.xml")
            .absolutePath
    toolVersion '8.13'
    ignoreFailures false
    showViolations true
}

task checkJavaStyle(type: Checkstyle) {
    description = "Java static code analysis."
    classpath = configurations.checkstyle
    source  'src'
    exclude '**/gen/**'
    exclude '**/test/**'
    exclude '**/build/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    if (hasCheckCommitParam() || isReleaseBuildType()) {
        def includeList = getCheckStyleIncludeList()
        if (includeList.size() == 0) {
            exclude '*'
        } else {
            include includeList
        }
    } else {
        include '**/*.java'
    }
    classpath = files()
}

checkJavaStyle.onlyIf {
    if (hasCheckCommitParam() || isReleaseBuildType()) {
        !getCheckStyleIncludeList().isEmpty()
    } else {
        true
    }
}

def getCheckStyleIncludeList() {
    def filterFileNameList = filterChangedFiles(getGitStatusFiles(), ".java")
    def includeList = new ArrayList()
    for (int i = 0; i < filterFileNameList.size(); i++) {
        String spliter = filterFileNameList[i]
        String[] spliterList = spliter.split("/")
        String fileName = spliterList[spliterList.length - 1]
        includeList.add("**/" + fileName)
    }
    return includeList
}

/**
 * gradle 生命周期
 * 1、初始化阶段
 * 2、配置阶段
 * 3、执行阶段
 *
 * afterEvaluate： Project配置结束后调用
 */
afterEvaluate {
    def prebuild = tasks.findByName("preBuild")
    if (prebuild != null) {
        if (isReleaseBuildType()) {
            // Release 注册deteKt, checkJavaStyle
            prebuild.dependsOn("detekt", "checkJavaStyle")
        } else {
            // DEBUG 注册gitHooks
            if (project.name.equalsIgnoreCase("app")) {
                prebuild.dependsOn("installGitHooks")
            }
        }
    }
}



